@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
if(UNIX AND NOT APPLE)
  find_dependency(Threads)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/dartvm_embed_libTargets.cmake")

set(DARTVM_EMBED_AVAILABLE_FLAVORS "jit;aot")
set(DARTVM_EMBED_DEFAULT_FLAVOR "jit")
set(DARTVM_EMBED_DARTSDK_BUILD_DIR "@DARTSDK_BUILD_DIR@")
set(_DARTVM_EMBED_BUNDLED_OUT_ROOT
  "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATADIR@/dartvm_embed_lib/dart-sdk/out/@DARTSDK_BUILD_DIR@")
set(_DARTVM_EMBED_BUNDLED_SDK_ROOT
  "${_DARTVM_EMBED_BUNDLED_OUT_ROOT}/dart-sdk")
set(_DARTVM_EMBED_BUNDLED_DART_BIN
  "${_DARTVM_EMBED_BUNDLED_SDK_ROOT}/bin/dart")
set(_DARTVM_EMBED_BUNDLED_DARTAOTRUNTIME_BIN
  "${_DARTVM_EMBED_BUNDLED_SDK_ROOT}/bin/dartaotruntime")
set(_DARTVM_EMBED_BUNDLED_GEN_SNAPSHOT_BIN
  "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATADIR@/dartvm_embed_lib/runtime/@DARTSDK_BUILD_DIR@/gen_snapshot")
set(_DARTVM_EMBED_BUNDLED_GEN_KERNEL_DART
  "${_DARTVM_EMBED_BUNDLED_SDK_ROOT}/bin/snapshots/gen_kernel_aot.dart.snapshot")
set(_DARTVM_EMBED_BUNDLED_VM_PLATFORM_DILL
  "${_DARTVM_EMBED_BUNDLED_SDK_ROOT}/lib/_internal/vm_platform_strong.dill")

set(DARTVM_EMBED_DARTSDK_ROOTDIR "${_DARTVM_EMBED_BUNDLED_OUT_ROOT}")

set(_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT
  "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATADIR@/dartvm_embed_lib/runtime/@DARTSDK_BUILD_DIR@")
set(_DARTVM_EMBED_BUNDLED_AOT_RUNTIME_LIB
  "${_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT}/libdart_embedder_runtime_aot_precompiled_static.a")
set(_DARTVM_EMBED_BUNDLED_JIT_RUNTIME_LIB
  "${_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT}/libdart_embedder_runtime_jit_static.a")
set(_DARTVM_EMBED_BUNDLED_LIBCXX
  "${_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT}/libc++.a")
set(_DARTVM_EMBED_BUNDLED_LIBUNWIND
  "${_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT}/libunwind.a")
set(_DARTVM_EMBED_BUNDLED_JIT_EXTRA_OBJ
  "${_DARTVM_EMBED_BUNDLED_RUNTIME_ROOT}/dart_set.dartdev_isolate.o")

foreach(_required_file
  "${_DARTVM_EMBED_BUNDLED_DART_BIN}"
  "${_DARTVM_EMBED_BUNDLED_DARTAOTRUNTIME_BIN}"
  "${_DARTVM_EMBED_BUNDLED_GEN_SNAPSHOT_BIN}"
  "${_DARTVM_EMBED_BUNDLED_GEN_KERNEL_DART}"
  "${_DARTVM_EMBED_BUNDLED_VM_PLATFORM_DILL}"
  "${_DARTVM_EMBED_BUNDLED_AOT_RUNTIME_LIB}"
  "${_DARTVM_EMBED_BUNDLED_JIT_RUNTIME_LIB}"
  "${_DARTVM_EMBED_BUNDLED_LIBCXX}"
  "${_DARTVM_EMBED_BUNDLED_LIBUNWIND}"
  "${_DARTVM_EMBED_BUNDLED_JIT_EXTRA_OBJ}")
  if(NOT EXISTS "${_required_file}")
    message(FATAL_ERROR
      "dartvm_embed_lib package is incomplete. Missing required bundled file: ${_required_file}")
  endif()
endforeach()

set(DARTVM_EMBED_DART_BIN "${_DARTVM_EMBED_BUNDLED_DART_BIN}")
set(DARTVM_EMBED_DARTAOTRUNTIME_BIN "${_DARTVM_EMBED_BUNDLED_DARTAOTRUNTIME_BIN}")
set(DARTVM_EMBED_GEN_SNAPSHOT_BIN "${_DARTVM_EMBED_BUNDLED_GEN_SNAPSHOT_BIN}")
set(DARTVM_EMBED_GEN_KERNEL_DART "${_DARTVM_EMBED_BUNDLED_GEN_KERNEL_DART}")
set(DARTVM_EMBED_VM_PLATFORM_DILL "${_DARTVM_EMBED_BUNDLED_VM_PLATFORM_DILL}")

include("${CMAKE_CURRENT_LIST_DIR}/DartVmEmbedHelpers.cmake")

check_required_components(dartvm_embed_lib)
